system requirements qws_requirements
for qwarn::qwarn.generic
[
	val minStrength=85.0
	val minwarnings=0.0
	val minresp=0.50
	val maxallowedresponsetime=5.0
	val minQueueforComm=5.00
	val minQ= 25.00
	val maxSpeed=50.00
	val minData=3.00
	val threshQ=10.00
	
	requirement traffic_management_center : "Traffic management center monitors and controls traffic and the road network"
	[
		description this "System should continue to monitor and control traffic and road network"
		rationale "Manages a broad range of transportation facilities including freeway systems, rural and suburban highway system"
		mitigates "transportation facilties failure"
		see goal qws_Goals.g1
		issues "Integrity Level 3"
	]
	
	requirement  Roadside_Equipment:"Send messages to, and receive messages from, nearby vehicles using DSRC"
	[
		description this "Used to send messages to, and receive messages from, nearby vehicles using DSRC or other alternative wireless communications"
		rationale "Must operates from a fixed position and may be permanently deployed or a portable device"
		mitigates "Communication failures"
		see goal qws_Goals.g2
		issues "Integrity Level 2"
	]
	requirement  ITS_Roadway_Equipment:"Provides environmental monitoring"
	[
		description this "Provides environmental monitoring including sensors that measure road conditions, surface weather"
		rationale "It should control access to transportation infrastructure"
		mitigates "Environmental issues"
		see goal qws_Goals.g3
		issues "Integrity Level 2"
	]
	requirement  Vehicle_Databus:"Enable communication between the Vehicle OBE and other vehicle systems"
	[
		description this "Enable communication between the Vehicle OBE and other vehicle systems to support connected vehicle application"
		rationale "The vehicle databus interface is not standardized across all vehicle classes"
		mitigates "Communication failures"
		see goal qws_Goals.g4
		issues "Integrity Level 5"
	]
	requirement  Vehicle_OBE:"Provides the vehicle-based processing, storage"
	[
		description this "Provides the vehicle-based processing, storage, and communications functions necessary to support connected vehicle operation"
		rationale "Vehicle OBE includes the common interfaces and functions that apply to all motorized vehicles"
		mitigates "Interfaces and function failures"
		see goal qws_Goals.g5
		issues "Integrity Level 4"
	]
	requirement fail_operational : "The system must continue to operate even in the case of failure of certain components"
	[
		description this "system must be able to operate even in the event of failure minimizing harm to other systems"
		rationale "the control systems may fail and this system must cope by either going to a safe state or by operating with other systems"
		mitigates "damage caused due to failure of components"
		see goal qws_Goals.ng1 qws_Goals.ng2 qws_Goals.g1
		issues "Integrity Level 5"
	]
	
	requirement individulized_warning : "The q-warn system must generate individualized queue warnings"
	[	
		compute num_warnings : real
		value predicate num_warnings > minwarnings
		description this "The queue warnings must be issued based on vehicle's distance to the end of the queue"
		rationale "Only when a vehicle is at certain distance from the queue, should it get a warning "
		mitigates "rear end collisions"
		see goal qws_Goals.ng3 qws_Goals.g3
		issues "Integrity Level 4"
	]
	
	
	requirement accurate_system : "The system shall handle accurate values of vehicle speed, location, position in queue"
	[
		description this "system shall handle high precision values of vehicle speed, location"
		rationale "In a life-critical system, a small change in the precision and accuracy can be vital"
		mitigates "collision in queues"
		see goal qws_Goals.ng8 qws_Goals.g4
		issues "Integrity Level 3"
	]
	
	
	requirement signal_strength : "The sensors along with the vehicle OBE shall have high signal strength in terms of receiving information"
	[
		description this "sensors should be capable of sensing data to a high degree of precision"
		
		compute signalStrength:real
		value predicate signalStrength >= minStrength
		rationale "the system is dependent on the sensors in order to make queue decisions and predictions"
		see goal qws_Goals.ng1 qws_Goals.ng5 qws_Goals.ng6
		issues "Integrity Level 4"
	]
	
	requirement reliable_system : "The system should have failure-free software operation for a specified period of time in a given environment"
	[
		description this "The system must operate without failure for a given amount of time in a given environment"
		rationale "In  this life-critical system, responsiveness and consistent operation according to its specification is required"
		see goal qws_Goals.ng1 qws_Goals.ng6 qws_Goals.ng13
		issues "Integrity Level 3"
	]
	
	
	requirement accurateSpeed : "The system shall support accurate values of velocity being recommended to the driver based  on distance to the queue dynamically "
	[
		description this "system shall recommend accurate speed values from the application in order to regulate the vehicle speed when it headed for a queue"
		compute queueDistance:real
		compute suggestedSpeed:real
		value predicate suggestedSpeed < maxSpeed && queueDistance < minQ
		rationale "Accuracy of speed values in this life-critical system is very important given the distance of the vehicle to the queue"
		mitigates "queue build up"
		see goal qws_Goals.ng8 qws_Goals.ng9 qws_Goals.g5
		issues "Integrity Level 3"
	]
	
	
	requirement predictQueue : "The system shall predict impending queues"
	[
		compute predQ:real
		value predicate predQ >= threshQ
		description this "The system shall correctly predict the formation of queues based on data collected from other vehicles and infrastructure systems"
		rationale "it can inform the driver of a vehicle about a queue that can be formed later"
		mitigates "queue formation"
		see goal qws_Goals.ng3
		issues "Integrity Level 3"
	]
	
	requirement interoperable_system : "The system must operate well with other systems in order to achieve its function"
	[
		description this "system must be able to operate with input and output from other systems for its functioning"
		rationale "the system is constantly going to have to operate with other systems for various values, parameters and readings being passed"
		see goal qws_Goals.ng4 qws_Goals.ng11
		issues "Integrity Level 2"
	]
	
	requirement generate_respStrategies : "The system shall generate response strategies to the driver"
	[
		compute respStrategies:real
		value predicate respStrategies >= minresp
		description this "Based on queue information, measures such lane changing, speed reductions must be suggested to the driver"
		rationale "Only is response strategies are generated, can the driver take alternative courses of action to avoid queues "
		mitigates "traffic aggregation"
		see goal qws_Goals.ng3 qws_Goals.ng13
		issues "Integrity Level 4"
	]
	
	requirement dataCollection : "The system should be able to collect multiple types of data"
	[ 	
		compute num_data : real
		value predicate num_data >= minData
		description this "data such as traffic information, road and weather conditions, etc. must be collected"
		rationale "such external data is important as it influences queue decisions and lane choices"
		mitigates "queue prediction errors"
		see goal qws_Goals.ng5
		issues "Integrity Level 3"
	]
	
	requirement responseTime : "The system response should be fast"
 	[
  	description this "A system with faster response is desirable"
  	rationale "Response time will decide how much time it takes in sensing queues and predicting response strategies "
    compute observedresponsetime: real
    value predicate observedresponsetime <= maxallowedresponsetime
    mitigates "Slow response of System and communication delays"
    see goal qws_Goals.ng6 qws_Goals.ng7
    issues "Integrity Level 4"
    ]
	
	
	requirement upgradable_system : "The system shall be able to use the collected data to update its prediction algorithm"
 	[
    description this "based on the collected data, the system will have to update its queue prediction algorithm"
    rationale "since vehicles are continuously in motion and the number of vehicles keep changing, it is important that the system dynamically predicts responses"
    mitigates "performance loss in queue prediction algorithm"
    see goal qws_Goals.ng10
    issues "Integrity Level 2"
    ]
    
    
	requirement predicteQueueResolution : "The system shall predict the time taken for a queue to resolve"
 	[
    description this "the system will have to make a prediction based on how much time it thinks a queue will take to be resolved"
    rationale "this informs the driver and passengers which lane to take in order to avoid entering a queue"
    compute predictedResolutionTime: real
    value predicate predictedResolutionTime > 0
    mitigates "queue formation and probable queue collisions"
    see goal qws_Goals.ng1 qws_Goals.ng3
    issues "Integrity Level 3"
    ]
	
	requirement broadcastQueueInformation : "The system shall communicate and broadcast queue characteristic information such as the length of queue, lanes impacted, number of vehicles in a particular queue, etc. to the driver "
 	[
    description this "the application should communicate queue information such as the length of queue, lanes impacted, number of vehicles in a particular queue, etc. to the driver based on the vehicles distance from the end of the queue"
    rationale "based on the how far the vehicle is from the queue, queue information is communicated to the driver in order to make decisions"
    compute lengthofQueue: real
    value predicate lengthofQueue > minQueueforComm
    mitigates "queue formation and probable queue collisions"
    see goal qws_Goals.ng1 qws_Goals.ng3 qws_Goals.ng12
    issues "Integrity Level 4"
    ]
    
    requirement detectQueu : "The system shall correctly recognize queues when they are formed"
 	[
    description this "The Q-warn system must detect the existence of a queue in a particular lane if there are certain minimum number of vehicles in that lane "
    rationale "Only if the system is able to detect a queue, can it make predictions and generate response strategies for incoming vehicles"
    compute noOfVehicles : real
    value predicate noOfVehicles > minQ
    mitigates "rear end collisions"
    see goal qws_Goals.ng1 qws_Goals.ng3 qws_Goals.ng5
    issues "Integrity Level 5"
    ]
	
]
